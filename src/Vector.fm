type Vector <A: Type>                           ~ (size: Nat) {
  nil                                           ~ (size: Nat.zero),
  ext<size: Nat>(head: A, tail: Vector(A,size)) ~ (size: Nat.succ(size)),
}

Vector.head<A: Type, size: Nat>(vector: Vector(A, Nat.succ(size))): A
  case vector {
    nil: Unit.new
    ext: vector.head
  }: case vector.size { zero: Unit, succ: A }

Vector.tail<A: Type, size: Nat>(vector: Vector(A, Nat.succ(size))): Vector(A, size)
  case vector {
    nil: Unit.new
    ext: vector.tail
  }: case vector.size { zero: Unit, succ: Vector(A, Nat.pred(vector.size)) }

Vector.from_list<A: Type>(xs: List(A)): Vector(A, List.length<A>(xs))
  case xs {
    nil  : Vector.nil<A>
    cons : Vector.ext<A>(_, xs.head, Vector.from_list<A>(xs.tail))
  }!

Vector.main: Nat
  Vector.head<_,3>(Vector.from_list<_>([1, 2, 3]))
